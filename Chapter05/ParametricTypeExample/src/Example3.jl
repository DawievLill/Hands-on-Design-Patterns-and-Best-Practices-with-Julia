module Example3

export Asset, Investment, Equity, Cash, 
    Stock, StockOption, Trade, SingleTrade, PairTrade

using Dates: Date

# Abstract type hierarchy for personal assets
abstract type Asset end
abstract type Investment <: Asset end
abstract type Equity <: Investment end
abstract type Cash <: Asset end

# ===== Equity Instruments Types ===== 

# Define concrete types 
struct Stock <: Equity
    symbol::String
    name::String
end

# Types of stock options
@enum CallPut Call Put

struct StockOption <: Equity
    symbol::String
    type::CallPut
    strike::Float64
    expiration::Date
end

# ===== Trading Types =====

abstract type Trade end

# Types (direction) of the trade
@enum LongShort Long Short

struct SingleTrade{T <: Investment} <: Trade
    type::LongShort
    instrument::T
    quantity::Int
    price::Float64
end

# A pair trade consists of two equity legs.
# If we have N types of Equity, then we have N * N possible types
# dynamically generated by the Julia compiler.
struct PairTrade{T <: Investment, S <: Investment} <: Trade
    leg1::SingleTrade{T}
    leg2::SingleTrade{S}
end

# Regardless of the instrument being traded, the direction of 
# trade (long or short) determines the sign of the payment amount.
sign(t::SingleTrade) = t.type == Long ? 1 : -1

# market value of a trade is simply quantity times price
payment(t::SingleTrade) = sign(t) * t.quantity * t.price

# stock options are special
# by convention, each option contract refers to 100 underlying shares of the stock
payment(t::SingleTrade{StockOption}) = sign(t) * t.quantity * 100 * t.price

# Net payment for a pair trade
net_payment(t::PairTrade) = payment(t.leg1) + payment(t.leg2)

# Testing
function test()
    stock = Stock("AAPL", "Apple, Inc.")
    option = StockOption("AAPL", Put, 215.00, Date(2019, 6, 30))
    trade = PairTrade(
        SingleTrade(Long, stock, 100, 200.00), 
        SingleTrade(Short, option, 1, 2.70))
    @info "Long stock, short put" trade net_payment(trade)

    stock_aapl = Stock("AAPL", "Apple, Inc.")
    stock_ibm = Stock("IBM", "IBM")
    trade = PairTrade(
        SingleTrade(Long, stock_aapl, 100, 200.00),
        SingleTrade(Short, stock_ibm, 50, 100.00))
    @info "Long AAPL, short IBM" trade net_payment(trade)
end

end # module